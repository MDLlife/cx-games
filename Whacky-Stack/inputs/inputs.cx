package inputs

import "glfw"

import "app"
import "map"
import "shape"
import "space"



// mouse 
var MousePointer       Vec2
var MousePointerX64    f64
var MousePointerY64    f64
var mouseMoveThreshold f64 = 75.0D // for Whacky Stack's blocky movement 

// keys 
var	GLFW_KEY_W     i32 = 87
var	GLFW_KEY_A     i32 = 65
var	GLFW_KEY_S     i32 = 83
var	GLFW_KEY_D     i32 = 68

var GLFW_KEY_SPACE i32 = 32



func Setup () {
	str.print("inputs.Setup()")

	// callbacks 
	glfw.SetKeyCallback("window", "onKeyEvent")
	glfw.SetMouseButtonCallback("window", "onMouseButtonEvent")
	glfw.SetCursorPosCallback("window", "onCursorPosEvent")
}


func Update () {
	//str.print("inputs.Update()")
}


func GoLeft () (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_A), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_LEFT), 1) {
		out = true
	}
}
func GoRight () (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_D), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_RIGHT), 1) {
		out = true
	}
}
func GoDown () (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_S), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_DOWN), 1) {
		out = true
	}
}
func GoUp () (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_W), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_UP), 1) {
		out = true
	}
}

func Back (key i32) (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_ESCAPE), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_HOME), 1) {
		out = true
	}
}


// mouse buttons 
func onMouseButtonEvent (window str, button i32, action i32, mods i32) {
	if /* release */ action == 0 {
	} else if /* press */ action == 1 {
		printf("onMouseButtonEvent() - %f, %f \n", MousePointer.X, MousePointer.Y)

		if button == 0 { // LMB 
			shape.Rotate()
			shape.Rotate()
			shape.Rotate()
		} else
		if button == 1 { // RMB 
			shape.Rotate()
		} else { // MMB or extra buttons (would be unwise to require more than 3 buttons) 
		}
	}
}


// mouse position 
var oldX f32
var oldY f32
var oldX64 f64
var oldY64 f64
var deltaX f32
var deltaY f32
var deltaX64 f64
var deltaY64 f64
var hdsl f64 // horizontal distance since latest piece movement 
func onCursorPosEvent (window str, x f64, y f64) {
	oldX = MousePointer.X
	oldY = MousePointer.Y
	oldX64 = MousePointerX64
	oldY64 = MousePointerY64
	

	MousePointerX64 = x
	MousePointerY64 = y

	var mX f32
	mX = f32.div(f64.f32(x), i32.f32(app.ScreenSizeInPixels.X))
	mX = f32.mul(mX, f32.mul(f64.f32(space.ScreenEdgeR), 2.0))
	MousePointer.X = f32.add(f64.f32(space.ScreenEdgeL), mX)

	var mY f32
	mY = f32.div(f64.f32(y), i32.f32(app.ScreenSizeInPixels.Y))
	mY = f32.mul(mY, 2.0)
	MousePointer.Y = f32.sub(1.0, mY)
	

	deltaX = f32.sub(MousePointer.X, oldX)
	deltaY = f32.sub(MousePointer.Y, oldY)
	deltaX64 = f64.sub(x, oldX64)
	deltaY64 = f64.sub(y, oldY64)

	hdsl = f64.add(hdsl, deltaX64)
	
	if hdsl > mouseMoveThreshold {
		shape.PlayerPos.X = i32.add(shape.PlayerPos.X, 1)
		hdsl = 0.0D
	} else 
	if hdsl < -mouseMoveThreshold {
		shape.PlayerPos.X = i32.sub(shape.PlayerPos.X, 1)
		hdsl = 0.0D
	}	
	
	printf("onCursorPosEvent %f, %f \n", deltaX64, deltaY64)
	//printf("onCursorPosEvent %f, %f \n", MousePointer.X, MousePointer.Y)
}


// keys 
func onKeyEvent (window str, key i32, scancode i32, action i32, mods i32) () {
	// action == 0, release 
	// action == 1, press 
	// action == 2, is an autorepeat event, generated by keys that are held longer than a second

	if action != 2 { // (not a repeat) 
		if action == 1 { // (press) 
			//str.print("-------------------------------------PRESS (not a repeat)")

			if key == 53 { // GLFW_KEY_5 
				// close 
				str.print("^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ QUITTING GAME ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^")
				glfw.SetShouldClose("window", true)
			} else
			if eventingLeft(key) {
				shape.Rotate()
				shape.Rotate()
				shape.Rotate()
			} else
			if eventingRight(key) {
				shape.Rotate()
			} else
			if eventingUp(key) {
				shape.PlayerPos.Y = i32.add(shape.PlayerPos.Y, 1)
			} else
			if eventingDown(key) {
				shape.PlayerPos.Y = i32.sub(shape.PlayerPos.Y, 1)
			}

			maybeToggleMousePointer(key)
		}
	}
}


var wantMousePointer bool
func maybeToggleMousePointer (key i32) () {
	i32.print(key)

	if Back(key) ||
		key == 257 || // GLFW_KEY_ENTER
		key == 258 || // GLFW_KEY_TAB 
		key == 259 { // GLFW_KEY_BACKSPACE 

		str.print("DEF go to main menu")
		wantMousePointer = !wantMousePointer

		if bool.eq(wantMousePointer, true) {
			str.print("wantMousePointer TRUE")
			glfw.SetInputMode("window", glfw.Cursor, glfw.CursorNormal)
		} else {
			str.print("wantMousePointer FALSE")
			glfw.SetInputMode("window", glfw.Cursor, glfw.CursorDisabled)
		}
	}
}


func eventingLeft(key i32) (out bool) {
  if i32.eq(key, GLFW_KEY_A) ||
    i32.eq(key, GLFW_KEY_LEFT) {
    out = true
  }
}

func eventingRight(key i32) (out bool) {
  if i32.eq(key, GLFW_KEY_D) ||
    i32.eq(key, GLFW_KEY_RIGHT) {
    out = true
  }
}

func eventingDown(key i32) (out bool) {
  if i32.eq(key, GLFW_KEY_S) ||
    i32.eq(key, GLFW_KEY_DOWN) {
    out = true
  }
}

func eventingUp(key i32) (out bool) {
  if i32.eq(key, GLFW_KEY_W) ||
    i32.eq(key, GLFW_KEY_UP) {
    out = true
  }
}
