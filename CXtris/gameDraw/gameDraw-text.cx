package gameDraw

import "inputs"
import "menu"



// color channel intensities 
var light f32 = 0.75
var mid   f32 = 0.5
var dark  f32 = 0.3



func InitFontsAndMenuItems() {
	gl.DepthFunc(gl.LESS)

	var file str = "../CXtris/assets/fonts/roboto-light.ttf"
	loadFontSized(file, f32.i32(menu.MainSize))
	loadFontSized(file, f32.i32(menu.OptionsSize))
	loadFontSized(file, 28)
	loadFontSized(file, 100)
	
	menu.Init()

	var nm i32 = len(menu.Items)    // nm == num menus 
	for m := 0; m < nm; m++ {       // m  == menu id 
	var ni i32 = len(menu.Items[m]) // ni == num items 
	for i := 0; i < ni; i++ {       // i  == item id 
		var font str = str.concat("Roboto", sprintf("%d", f32.i32(menu.Items[m][i].Size)))
		menu.Items[m][i].Font = font		
		var w i32
		var h i32
		w, h = gltext.Metrics(font, 
		menu.Items[m][i].Text)
		menu.Items[m][i].Wid = i32.f32(w)
		menu.Items[m][i].Hei = i32.f32(h)
		menu.Items[m][i].Left = (i32.f32(app.START_WIDTH) - menu.Items[m][i].Wid) / 2.0
		
		printf("%s ---- %d,%d ---- '%s' \n", font, w, h, menu.Items[m][i].Text)
	}
	}
}


func loadFontSized (fn str, size i32) { // filename 
	var s str = str.concat("Roboto", sprintf("%d", size))
	
	os.Open(fn)
	gltext.LoadTrueType(s, fn, size, 32, 127, gltext.LeftToRight)
	os.Close(fn)
}


func allText () {
	drawOutlinedText(app.Name, 18.0, i32.f32(app.START_WIDTH / 2), 1.0)
	
	if app.Mode <= app.MODE_MENU_OPTIONS { // in a menu 
		menuRects()
		
		gl.Enable(gl.DEPTH_TEST)
			menuTexts()
			scoreText()
		gl.Disable(gl.DEPTH_TEST)
	}else{
		gl.Enable(gl.DEPTH_TEST)
			scoreText()
			
			if app.Mode == app.MODE_PLAYING {
				//var numKeysHori i32 = 3
				//for i := 0; i < numKeysHori; i++ {}
				//"mouse" icons/s needed 
				var scale f32 = 0.6
				drawOutlinedText("[A]", 0.0, 0.0, scale)
				drawOutlinedText("[S]", i32.f32(app.START_WIDTH) * 0.33, -20.0, scale)
				drawOutlinedText("[D]", i32.f32(app.START_WIDTH) * 0.66, -40.0, scale)
	
					//s = s + "ROTATE Left  == LMB, [Q], [LCtrl], [RAlt]\n"
					//s = s + "ROTATE Right == RMB, [E], [LAlt],  [RCtrl]\n"
			}else
			if app.Mode == app.MODE_GAME_OVER {
				drawOutlinedText("GAME OVER", 18.0, i32.f32(app.START_WIDTH / 2), 1.0)
			}
		gl.Disable(gl.DEPTH_TEST)
	}
}


func menuTexts () {
	var m i32 = app.Mode
	gl.LoadIdentity()

	// greater y (value) 
	var gY f32 = i32.f32(app.START_HEIGHT - 1) 

	///////////////drawOutlinedText(5.0)

	for i := i32.sub(len(menu.Items[m]), 1); i >= 0; i-- {
		var y f32 = f32.sub(gY, menu.Items[m][i].Hei)

		if inputs.PointerInYPixelRange(y, gY) {
			menu.Items[m][i].Hover = true			
			gl.Color3f(1.0, 1.0, 1.0)
		}else{
			menu.Items[m][i].Hover = false
			gl.Color3f(mid, mid, mid)
		}

		gltext.Printf(
			menu.Items[m][i].Font, 
			menu.Items[m][i].Left, 
			y, 
			menu.Items[m][i].Text)
			
		gY = y
	}
}


func scoreText () {
	var sX f32 = i32.f32(i32.sub(app.START_WIDTH, 59))
	var sY f32 = i32.f32(93) //app.START_HEIGHT)
	gl.LoadIdentity()
	gl.Color3f(1.0, 1.0, 1.0)
	gltext.Printf("Roboto28", sX, sY, shape.PlayerScoreTxt)
}


func drawOutlinedText (s str, x f32, y f32, scale f32) {
	var alpha f32 = 1.0
	gl.Scalef(scale, scale, 0.0)

	// background shadow 
	printTextWithColor(s, x+1.0, y+1.0, 0.0, 0.0, 0.0, alpha)
	printTextWithColor(s, x-1.0, y+1.0, 0.0, 0.0, 0.0, alpha)
	printTextWithColor(s, x+1.0, y-1.0, 0.0, 0.0, 0.0, alpha)
	printTextWithColor(s, x-1.0, y-1.0, 0.0, 0.0, 0.0, alpha)
	// main filling on top 
	printTextWithColor(s, x,     y,     1.0, 0.5, 0.0, alpha)
}


// x & y are offsets 
func printTextWithColor (s str, x f32, y f32, r f32, g f32, b f32, a f32) {
	gl.Color4f(r, g, b, a)
	gltext.Printf("Roboto100", x, y, s)
}
