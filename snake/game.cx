package game

import "ccInput"
import "entity"



// TODO: 
// 
// * optimize snake segment drawing (either 
//			4 oriented quads or 
//			unoriented lookup table of sphere rim points) 
// * juice up ResetEventText with blip of white (or light-violet), & shaking? 



var Name str = "  S  N  A  K  E  "
var NumRadianGrains i32 = 256 // gives 64 aim/angle/direction possibilities per quadrant 
var RadianGrainForRNG f32
var AsteroidVelocityGrain f32 = 0.00002
var BulletRadius f32 = 0.015
var timeDelta f32

// full spectrum cos/sin angle/radius test visualizers 
var xOfBoxyDistance        f32
var yOfBoxyDistance        f32
var xOfPerfectRadius       f32 // current pos 
var yOfPerfectRadius       f32 // current pos 
var visualizerAngle        f32
var visualizerRadius       f32 = 0.6
var visualizerFrameAdvance f32 = 0.3

// screen 
var ScreenNumPixelsWide i32 = 1280
var ScreenNumPixelsHigh i32 = 720
var ScreenBoundsTop f32
var ScreenBoundsRight f32
var ScreenBoundsBottom f32
var ScreenBoundsLeft f32

// snake 
var MinSnakeExtent f32 = 0.1
var MaxSnakeExtent f32 = 0.35
var CurrSnakeExtent f32 = 0.17
var CurrSnakeAim f32 = 0.0
var TailRadius f32 = 0.02
var HeadRadius f32 = 0.07
var shrinkColorIntensity f32
var growColorIntensity f32
var rotateNegativelyColorIntensity f32
var rotatePositivelyColorIntensity f32

// game state 
var currTime           f32 = 0.0
var GAMESTATE_Playing  i32 = 0
var GAMESTATE_GameOver i32 = 1
var GAMESTATE_Menu     i32 = 2
var Mode               i32
var Stage              i32 = 1 // waves that get progressively harder, like 80s arcade games 

// temp/event/announcement text 
var TempText               str
var TempTextDuration       f32 = 2.5
var TempTextTimeFadeStarts f32
var TempTextColorIntensity f32
var TempTextX              f32
var TempTextY              f32
var tempTextMiddleX        f32



func Init() () {
	updateTiming()
	RadianGrainForRNG = 2.0 * 3.141592654 / i32.f32(NumRadianGrains)

	// event/announcement text 
	TempText = Name
	tempTextMiddleX = i32.f32(ScreenNumPixelsWide) / 2.0
	ResetTempText()


	// screen 
	// (vertical portion of aspect ratio is always 1.0 (in the movie industry) ) 
	extentXFromOrigin := i32.f32(ScreenNumPixelsWide) / i32.f32(ScreenNumPixelsHigh)
	ScreenBoundsTop = 1.0
	ScreenBoundsRight = extentXFromOrigin
	ScreenBoundsBottom = -1.0
	ScreenBoundsLeft = 0.0 - extentXFromOrigin

	entity.Init()
	str.print("--- game.Init()")
}


var pixelRT f32 // running total for pixel movement 
func Update() () {
	//setPosOfVisualizers()
	updateTiming()
	fadeDirectionalIndicatorsColorIntensity()
	ccInput.Update() 
	entity.Update()

	// text events/announcements 
	if currTime > TempTextTimeFadeStarts {
		TempTextColorIntensity -= timeDelta * 0.5
		pixelRT                += timeDelta * 50.0
		
		for pixelRT > 1.0 {
			pixelRT -= 1.0

			TempTextX   -= 1.0
			TempTextY   += 1.0
		}
	}
}


var prevFrameTime f32
func updateTiming() () {
	currTime = f64.f32(glfw.GetTime())
	timeDelta = currTime - prevFrameTime
	prevFrameTime = currTime
}


func fadeDirectionalIndicatorsColorIntensity() () {
	rotatePositivelyColorIntensity = fadeColorIntensity(rotatePositivelyColorIntensity)
	rotateNegativelyColorIntensity = fadeColorIntensity(rotateNegativelyColorIntensity)
	growColorIntensity = fadeColorIntensity(growColorIntensity)
	shrinkColorIntensity = fadeColorIntensity(shrinkColorIntensity)
}


func fadeColorIntensity(f f32) (out f32) {
	if f > 0.0 {
		f -= (timeDelta * 1.4)
	}

	return f
}


var divisor f32 = 1000.0
func SetSnakeAimAndSize(deltaX f32, deltaY f32) () {
	rotateMul := 21.5
	div := 90.0
	rotX, rotY := RotatePositionByAngle(deltaX/div, deltaY/div, CurrSnakeAim)

	CurrSnakeAim    += rotX
	CurrSnakeExtent += rotY / 19.0

	if CurrSnakeExtent > MaxSnakeExtent {
		CurrSnakeExtent = MaxSnakeExtent
	}

	if CurrSnakeExtent < MinSnakeExtent {
		CurrSnakeExtent = MinSnakeExtent
	}



	// set directional indicator colors 
	if rotX > 0.0 {
		rotatePositivelyColorIntensity += rotX * timeDelta * rotateMul
		if rotatePositivelyColorIntensity > 1.0 {
			rotatePositivelyColorIntensity = 1.0
		}
	} else {
		rotatePositivelyColorIntensity = 0.0
	}

	if rotX < 0.0 {
		rotateNegativelyColorIntensity += 0.0 - rotX * timeDelta * rotateMul
		if rotateNegativelyColorIntensity > 1.0 {
			rotateNegativelyColorIntensity = 1.0
		}
	} else {
		rotateNegativelyColorIntensity = 0.0
	}

	if rotY > 0.0 {
		growColorIntensity += rotY * timeDelta * rotateMul
		if growColorIntensity > 1.0 {
			growColorIntensity = 1.0
		}
	} else {
		growColorIntensity = 0.0
	}

	if rotY < 0.0 {
		shrinkColorIntensity += 0.0 - rotY * timeDelta * rotateMul
		if shrinkColorIntensity > 1.0 {
			shrinkColorIntensity = 1.0
		}
	} else {
		shrinkColorIntensity = 0.0
	}



	//str.print(str.concat("deltaX: ", f32.str(deltaX)))
	//str.print(str.concat("deltaY: ", f32.str(deltaY)))
	//str.print(str.concat("CurrSnakeAim: ", f32.str(CurrSnakeAim)))
	//str.print(str.concat("CurrSnakeExtent: ", f32.str(CurrSnakeExtent)))
}


func GetShrinkColorIntensity() (out f32) {
	return shrinkColorIntensity
}


func GetGrowColorIntensity() (out f32) {
	return growColorIntensity
}


func GetRotateNegativelyColorIntensity() (out f32) {
	return rotateNegativelyColorIntensity
}


func GetRotatePositivelyColorIntensity() (out f32) {
	return rotatePositivelyColorIntensity
}


func GetDistance(aX f32, aY f32, bX f32, bY f32) (out f32) {
	xOff := bX - aX
	yOff := bY - aY
	return f32.pow((xOff ** 2.0) + (yOff ** 2.0), 0.5)
}


func RotatePositionByAngle(posX f32, posY f32, angle f32) (x f32, y f32) {
	s := f32.sin(angle)
	c := f32.cos(angle)

	return
		posX * c - posY * s,
		posX * s + posY * c
}


func GetPosOfPerfectRadiusVisualizer() (x f32, y f32) {
	x = xOfPerfectRadius
	y = yOfPerfectRadius
}


func GetPosOfBoxyDistanceVisualizer() (x f32, y f32) {
	x = xOfBoxyDistance
	y = yOfBoxyDistance
}


func SetTempText(s str) () {
	TempText = s
}


func SetStage(stage i32) () {
	Stage = stage
}


func ResetTempText() () {
	TempTextTimeFadeStarts = currTime + TempTextDuration
	TempTextColorIntensity = 1.0
	TempTextX = tempTextMiddleX
	TempTextY = 0.0
}



// private 

func setPosOfVisualizers() () {
	xOfPerfectRadius, 
	yOfPerfectRadius = 
		f32.sin(visualizerAngle) * visualizerRadius, 
		f32.cos(visualizerAngle) * visualizerRadius

	bd := 
		f32.abs(xOfPerfectRadius) + 
		f32.abs(yOfPerfectRadius)

	xOfBoxyDistance, 
	yOfBoxyDistance = 
		f32.sin(visualizerAngle) * bd, 
		f32.cos(visualizerAngle) * bd

	visualizerAngle += 
	visualizerFrameAdvance
}
