/*
 Based on code from https://kylewbanks.com/blog/tutorial-opengl-with-golang-part-3-implementing-the-game
*/

package main

import "gl"
import "glfw"

var width i32 = 600
var height i32 = 600
var rows i32 = 15
var columns i32 = 15

var vertexShaderSource str = "
          #version 120
          void main() {
            gl_Position = gl_ProjectionMatrix * gl_ModelViewMatrix * gl_Vertex;
          }
"

var fragmentShaderSource str = "
          #version 120
          void main() {
            gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
          }
"

type Cell struct {
	drawable i32

	alive bool
	aliveNext bool
	
	x i32
	y i32
}

var square []f32 = []f32{
	-0.5, 0.5, 0.0,
	-0.5, -0.5, 0.0,
	0.5, -0.5, 0.0,

	-0.5, 0.5, 0.0,
	0.5, 0.5, 0.0,
	0.5, -0.5, 0.0
}

func map2Dto1D (r i32, c i32, w i32) (i i32) {
	i = i32.add(i32.mul(w, r), c)
}

func map1Dto2D (i i32, w i32) (c i32, r i32) {
	r = i32.sub(i32.sub(w, 1), i32.div(i, w))
	c = i32.mod(i, w)
}

func wrapX (x i32) (newX i32) {
	if i32.eq(x, columns) {
		newX = 0
		return
	}
	if i32.eq(x, -1) {
		newX = i32.sub(columns, 1)
		return
	}
	newX = x
}

func wrapY (y i32) (newY i32) {
	if i32.eq(y, rows) {
		newY = 0
		return
	}
	if i32.eq(y, -1) {
		newY = i32.sub(rows, 1)
		return
	}
	newY = y
}

func aliveNeighbours (cell Cell, cells []Cell) (aliveCount i32) {
	upCellX := cell.x
	upCellY := wrapY(i32.add(cell.y, 1))
	upRightCellX := wrapX(i32.add(cell.x, 1))
	upRightCellY := wrapY(i32.add(cell.y, 1))
	rightCellX := wrapX(i32.add(cell.x, 1))
	rightCellY := cell.y
	downRightCellX := wrapX(i32.add(cell.x, 1))
	downRightCellY := wrapY(i32.add(cell.y, -1))
	downCellX := cell.x
	downCellY := wrapY(i32.add(cell.y, -1))
	downLeftCellX := wrapX(i32.add(cell.x, -1))
	downLeftCellY := wrapY(i32.add(cell.y, -1))
	leftCellX := wrapX(i32.add(cell.x, -1))
	leftCellY := cell.y
	upLeftCellX := wrapX(i32.add(cell.x, -1))
	upLeftCellY := wrapY(i32.add(cell.y, 1))
	
	upCell := []Cell.read(cells, map2Dto1D(upCellX, upCellY, columns))
	upRightCell := []Cell.read(cells, map2Dto1D(upRightCellX, upRightCellY, columns))
	rightCell := []Cell.read(cells, map2Dto1D(rightCellX, rightCellY, columns))
	downRightCell := []Cell.read(cells, map2Dto1D(downRightCellX, downRightCellY, columns))
	downCell := []Cell.read(cells, map2Dto1D(downCellX, downCellY, columns))
	downLeftCell := []Cell.read(cells, map2Dto1D(downLeftCellX, downLeftCellY, columns))
	leftCell := []Cell.read(cells, map2Dto1D(leftCellX, leftCellY, columns))
	upLeftCell := []Cell.read(cells, map2Dto1D(upLeftCellX, upLeftCellY, columns))

	if upCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if upRightCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if rightCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if downRightCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if downCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if downLeftCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if leftCell.alive {aliveCount = i32.add(aliveCount, 1)}
	if upLeftCell.alive {aliveCount = i32.add(aliveCount, 1)}

}

func changeState (cell Cell, cells []Cell) (newCell Cell) {
	cell.alive = cell.aliveNext
	cell.aliveNext = cell.alive

	aliveCount := aliveNeighbours(cell, cells)

	if cell.alive {
		// 1. Any live cell with fewer than two live neighbours dies, as if caused by underpopulation.
		if i32.lt(aliveCount, 2) {
			cell.aliveNext = false
		}
		// 2. Any live cell with two or three live neighbours lives on to the next generation.
		if or(i32.eq(aliveCount, 2), i32.eq(aliveCount, 3)) {
			cell.aliveNext = true
		}
		// 3. Any live cell with more than three live neighbours dies, as if by overpopulation.
		if i32.gt(aliveCount, 3) {
			cell.aliveNext = false
		}
	} else {
		// 4. Any dead cell with exactly three live neighbours becomes a live cell, as if by reproduction.
		if i32.eq(aliveCount, 3) {
			cell.aliveNext = true
		}
	}

	newCell = cell
}

func addNull (source str) (s str) {
	bSource := str.[]byte(source)
	bSource := []byte.append(bSource, i32.byte(0))
	s = []byte.str(bSource)
}

func compileShader(source str, shaderType i32) (shader i32) {
	shader := gl.CreateShader(shaderType)

	gl.Strs(source, "csources")
	gl.ShaderSource(shader, 1, "csources")
	gl.Free("csources")
	gl.CompileShader(shader)
}

func makeVao (points []f32) (vao i32) {
	var vbo i32
	gl.GenBuffers(1, vbo) // should be &vbo. to be fixed, but should work as it is
	gl.BindBuffer(gl.ARRAY_BUFFER, vbo)
	gl.BufferData(gl.ARRAY_BUFFER, i32.mul(4, []f32.len(points)), points, gl.STATIC_DRAW)

	var vao i32
	
	gl.GenVertexArrays(1, vao)
	gl.BindVertexArray(vao)
	gl.EnableVertexAttribArray(0)
	gl.BindBuffer(gl.ARRAY_BUFFER, vbo)
	gl.VertexAttribPointer(0, 3, gl.FLOAT, false, 0)
}

func newCell (x i32, y i32) (cell Cell) {
	points := []f32.make([]f32.len(square))
	[]f32.copy(points, square)

	for i := 0; i32.lt(i, []f32.len(points)); i = i32.add(i, 1) {
		var position f32
		var size f32

		if i32.eq(i32.mod(i, 3), 0) {
			size = f32.div(1.0, i32.f32(columns))
			position = f32.mul(i32.f32(x), size)
		}
		if i32.eq(i32.mod(i, 3), 1) {
			size = f32.div(1.0, i32.f32(rows))
			position = f32.mul(i32.f32(y), size)
		}

		if f32.lt([]f32.read(points, i), 0.0) {
			points = []f32.write(points, i, f32.sub(f32.mul(position, 2.0), 1.0))
		} else {
			points = []f32.write(points, i, f32.sub(f32.mul(f32.add(position, size), 2.0), 1.0))
		}
	}

	cell = new Cell{
		drawable: makeVao(points),
		x: x,
		y: y
	}
}

func makeCells () (cells []Cell) {
	var cells []Cell
	for x := 0; i32.lt(x, rows); x = i32.add(x, 1) {
		for y := 0; i32.lt(y, columns); y = i32.add(y, 1) {
			cell := newCell(x, y)
			cell.alive = i32.lt(i32.rand(0, 100), 15)
			cell.aliveNext = cell.alive
			cells = []Cell.append(cells, cell)
		}
	}
}

func initGlfw (window str) () {
	glfw.Init()
	glfw.WindowHint(glfw.Resizable, glfw.False)
	glfw.WindowHint(glfw.ContextVersionMajor, 2)
	glfw.WindowHint(glfw.ContextVersionMinor, 1)

	glfw.CreateWindow("window", width, height, "Conway's Game of Life")
	glfw.MakeContextCurrent("window")
}

func initOpenGL () (program i32) {
	gl.Init()

	vertexShader := compileShader(addNull(vertexShaderSource), gl.VERTEX_SHADER)
	fragmentShader := compileShader(addNull(fragmentShaderSource), gl.FRAGMENT_SHADER)

	program := gl.CreateProgram()
	
	gl.AttachShader(program, vertexShader)
	gl.AttachShader(program, fragmentShader)
	gl.LinkProgram(program)
}

func drawCell (cell Cell) () {
	if not(cell.alive) {
		return
	}
	gl.BindVertexArray(cell.drawable)
	gl.DrawArrays(gl.TRIANGLES, 0, i32.div([]f32.len(square), 3))
}

func draw (cells []Cell, window str, program i32) () {
	gl.Clear(i32.bitor(gl.COLOR_BUFFER_BIT, gl.DEPTH_BUFFER_BIT))
	gl.UseProgram(program)

	for i := 0; i32.lt(i, i32.mul(rows, columns)); i = i32.add(i, 1) {
		drawCell([]Cell.read(cells, i))
	}

	glfw.PollEvents()
	glfw.SwapBuffers("window")
}

func main () () {
	initGlfw("window")
	program := initOpenGL()

	cells := makeCells()

	for not(glfw.ShouldClose("window")) {

		for c := 0; i32.lt(c, i32.mul(rows, columns)); c = i32.add(c, 1) {
			cells = []Cell.write(cells, c, changeState([]Cell.read(cells, c), cells))
		}
		
		draw(cells, "window", program)
	}
}
